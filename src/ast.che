use import std.printable
use import std.string
use import std.array

use import interpreter
use import lexer
use import scope
use import types
use import value

#export_scope

AstNodeFlags :: enum #flags {
    None
    LValue
    IsDefinedInScope
    AllowPubModifier
    OnlyPubDeclarations
}

AddressMode :: enum #copy {
    NotSet
    Address
    Value
}

__uiaeuiaeuiae := @type_info(AstNodeFlags)

AstNode :: trait {
    id       : int = -1
    location : Location
    scope    : ^mut Scope = null
    typ      : ^TypeInfo = null
    value    : Value = Value.None
    flags    : int = 0
}

impl AstNode {
    set_flags :: (&mut Self, flags: AstNodeFlags) {
        self.flags = @bin_or(self.flags, int(flags))
    }

    clear_flags :: (&mut Self, flags: AstNodeFlags) {
        self.flags = @bin_and(self.flags, @bin_not(int(flags)))
    }

    has_flags :: (&Self, flags: AstNodeFlags) -> bool {
        return @bin_and(self.flags, int(flags)) == int(flags)
    }
}

// statements
AstConstDecl :: struct AstNode {
    pattern     : &mut AstNode
    type_expr   : ^mut AstNode
    value_expr  : &mut AstNode

    name        := "_"
    public      : bool = false
    original_symbol := Option[Symbol].None

    var_type    : ^TypeInfo = null
}
impl AstNode for AstConstDecl {}

AstDecl :: struct AstNode {
    pattern     : ^mut AstNode
    type_expr   : ^mut AstNode
    value_expr  : ^mut AstNode

    mutable     : bool = false
    public      : bool = false

    name        : string = "_"
    var_type    : ^TypeInfo = null
}
impl AstNode for AstDecl {}

AstImport :: struct AstNode {
    path : &mut AstNode
}
impl AstNode for AstImport {}

AstUse :: struct AstNode {
    value_expr : &mut AstNode
}
impl AstNode for AstUse {}

AstImpl :: struct AstNode {
    target_expr : &mut AstNode
    trait_expr  : ^mut AstNode
    children    : Array[^mut AstNode]

    functions   := Array[^mut AstFunction].new(0)
    sub_scope   : ^mut Scope = null
    
    children_done := false // is true when all jobs for the children of this impl block are done
}
impl AstNode for AstImpl {}

// expressions

AstStruct :: struct AstNode {
    name        := "#anonymous"
    trait_expr  : ^mut AstNode
    children    : Array[^mut AstNode]

    sub_scope   : ^mut Scope = null
    struct_type : ^mut TypeInfo = null
    members     := Array[^mut AstDecl].create()
    constants   := Array[^mut AstConstDecl].create()

    // flags
    members_done := false
}
impl AstNode for AstStruct {}

AstTrait :: struct AstNode {
    name        := "#anonymous"
    children    : Array[^mut AstNode]

    sub_scope   : ^mut Scope = null
    trait_type  : ^TypeInfo = null
    members     := Array[^mut AstDecl].create()
    functions   := Array[^mut AstConstDecl].create()

    // flags
    members_done := false
}
impl AstNode for AstTrait {}

AstEnum :: struct AstNode {
    name        := "#anonymous"
    children    : Array[^mut AstNode]

    sub_scope   : ^mut Scope = null
    enum_type   : ^TypeInfo = null
    members     := Array[^mut AstDecl].create()

    // flags
    members_done := false
}
impl AstNode for AstEnum {}

AstFunction :: struct AstNode {
    params              : Array[^mut AstDecl]
    body                : ^mut AstNode
    return_type_expr    : ^mut AstNode

    param_scope         : ^mut Scope
    name                := "#anonymous"
    return_type         : ^TypeInfo = null

    bytecode            : ^mut CBCFunction = null
}
impl AstNode for AstFunction {}

AstPoly :: struct AstNode {
    params      : Array[^mut AstDecl]
    value_expr  : &mut AstNode

    param_scope : ^mut Scope
    name        := "#anonymous"
}
impl AstNode for AstPoly {}

AstArgument :: struct AstNode {
    name        : ^mut AstIdentifier
    value_expr  : &mut AstNode

    param       : enum #copy {
        None
        Parameter       : &AstDecl
        StructMember    : &TypeInfoStructMember
    }
}
impl AstNode for AstArgument {}

AstBlock :: struct AstNode {
    sub_scope   : ^mut Scope
    children    : Array[^mut AstNode]
}
impl AstNode for AstBlock {}

AstCast :: struct AstNode {
    type_expr   : ^mut AstNode
    value_expr  : &mut AstNode
}
impl AstNode for AstCast {}

AstCall :: struct AstNode {
    function        : &mut AstNode
    arguments       : Array[^mut AstArgument]
    ast_function    : ^AstFunction = null
}
impl AstNode for AstCall {}

AstIdentifier :: struct AstNode {
    name    : string
    symbol  := Symbol.NotFound
    original_symbol := Option[Symbol].None
}
impl AstNode for AstIdentifier {}

AstNumberLiteral :: struct AstNode {
}
impl AstNode for AstNumberLiteral {}

AstString :: struct AstNode {
    string_value : string
}
impl AstNode for AstString {}

AstChar :: struct AstNode {
    char_value : char
}
impl AstNode for AstChar {}

AstBool :: struct AstNode {
    bool_value : bool
}
impl AstNode for AstBool {}

AstNull :: struct AstNode {
    
}
impl AstNode for AstNull {}

AstBinary :: struct AstNode {
    operator    : BinOp
    left        : &mut AstNode
    right       : &mut AstNode

    BinOp :: enum #copy { Add, Sub, Mul, Div, Mod, And, Or, Less, LessEq, Greater, GreaterEq, Equal, NotEqual, Move, Pipe, Range, RangeIncl }
}
impl AstNode for AstBinary {}

AstUnary :: struct AstNode {
    operator    : UnOp
    sub         : &mut AstNode

    UnOp :: enum #copy { Neg, Not, Ref, RefMut, Ptr, PtrMut, Deref }
}
impl AstNode for AstUnary {}

AstAssignment :: struct AstNode {
    pattern     : &mut AstNode
    value_expr  : &mut AstNode
}
impl AstNode for AstAssignment {}

AstIf :: struct AstNode {
    condition   : &mut AstNode
    true_case   : &mut AstNode
    false_case  : ^mut AstNode
}
impl AstNode for AstIf {}

AstFor :: struct AstNode {
    it_pattern  : ^mut AstNode
    index_name  : ^mut AstIdentifier
    collection  : &mut AstNode
    body        : &mut AstNode
}
impl AstNode for AstFor {}

AstLoop :: struct AstNode {
    body : &mut AstNode
}
impl AstNode for AstLoop {}

AstMatch :: struct AstNode {
    value_expr  : ^mut AstNode
    cases       : Array[^mut AstMatchCase]
}
impl AstNode for AstMatch {}

AstMatchCase :: struct {
    pattern     : &mut AstNode
    condition   : ^mut AstNode
    body        : &mut AstNode
}

AstBreak :: struct AstNode {
    label       : ^mut AstIdentifier
    value_expr  : ^mut AstNode
}
impl AstNode for AstBreak {}

AstContinue :: struct AstNode {
    label : ^mut AstIdentifier
}
impl AstNode for AstContinue {}

AstReturn :: struct AstNode {
    value_expr : ^mut AstNode
}
impl AstNode for AstReturn {}

AstDefer :: struct AstNode {
    sub : &mut AstNode
}
impl AstNode for AstDefer {}

AstTuple :: struct AstNode {
    values : Array[^mut AstNode]
}
impl AstNode for AstTuple {}

AstArray :: struct AstNode {
    values : Array[^mut AstNode]
}
impl AstNode for AstArray {}

AstArrayType :: struct AstNode {
    count   : ^mut AstNode
    target  : &mut AstNode
}
impl AstNode for AstArrayType {}

AstDot :: struct AstNode {
    sub     : ^mut AstNode
    name    : &mut AstIdentifier

    symbol  := Symbol.NotFound
    original_symbol := Option[Symbol].None
    sub_type        : ^TypeInfo = null
    address_mode    := AddressMode.NotSet
}
impl AstNode for AstDot {}

AstIndex :: struct AstNode {
    sub         : &mut AstNode
    arguments   : Array[^mut AstArgument]
    index       : ^mut AstNode
}
impl AstNode for AstIndex {}

AstUfc :: struct AstNode {
    value_expr  : &mut AstNode
    symbol      : Symbol
}

// nodes generated by compiler
AstDeclRef :: struct AstNode {
    decl : enum {
        Decl        : &mut AstDecl
        ConstDecl   : &mut AstConstDecl
    }
}

// impl AstFunction {
//     get_parameter :: (&Self, name: string) -> Option[&AstDecl] {
//         for p in params {
//             match p.name {
//                 AstIdentifier($)
//             }
//             if p.name.name == name {
//                 return Some(*p)
//             }
//         }
//         return None
//     }
// }

impl AstNode {
    set_name :: (&Self, name: string) {
        match self {
            AstDecl($decl) -> {
                decl.name = name
            }
            AstConstDecl($decl) -> {
                decl.name = name
            }
            AstFunction($func) -> {
                func.name = name
            }
            AstStruct($str) -> {
                str.name = name
            }
            AstEnum($en) -> {
                en.name = name
            }
            AstTrait($tr) -> {
                tr.name = name
            }
            AstPoly($poly) -> {
                poly.name = name
            }
        }
    }

    get_symbol :: (&Self) -> Option[Symbol] {
        return match self {
            AstIdentifier($id) -> Some(id.symbol)
            AstDot($dot) -> Some(dot.symbol)
            AstUfc($ufc) -> Some(ufc.symbol)
            _ -> None
        }
    }

    get_original_symbol :: (&Self) -> Option[Symbol] {
        return match self {
            AstStruct(_)        -> Some(Symbol.OriginalNode(self))
            AstEnum(_)          -> Some(Symbol.OriginalNode(self))
            AstTrait(_)         -> Some(Symbol.OriginalNode(self))
            AstConstDecl($decl) -> decl.original_symbol
            AstIdentifier($id)  -> id.original_symbol
            AstDot($dot)        -> dot.original_symbol
            // AstUfc($ufc)     -> Some(ufc.original_symbol)
            _                   -> None
        }
    }
}

impl AstBinary.BinOp {
    lexeme :: (Self) -> string {
        return match self {
            .Add        -> "+"
            .Sub        -> "-"
            .Mul        -> "*"
            .Div        -> "/"
            .Mod        -> "%"
            .And        -> "and"
            .Or         -> "or"
            .Less       -> "<"
            .LessEq     -> "<="
            .Greater    -> ">"
            .GreaterEq  -> ">="
            .Equal      -> "=="
            .NotEqual   -> "!="
            .Move       -> "<-"
            .Pipe       -> "|"
            .Range      -> ".."
            .RangeIncl  -> "..="

            _ -> @assert(false)
        }
    }
}

impl AstUnary.UnOp {
    lexeme :: (Self) -> string {
        return match self {
            .Neg    -> "-"
            .Not    -> "!"
            .Ref    -> "&"
            .RefMut -> "&mut "
            .Ptr    -> "^"
            .PtrMut -> "^mut "
            .Deref  -> "*"
            _       -> @assert(false)
        }
    }
}
